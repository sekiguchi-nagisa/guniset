#!/usr/bin/env arsh

# generate golden files for script (except for 'Unknown')

var REV="16.0.0"

let output_dir = $1.realpath()

test -f Scripts.txt ||
  curl "https://www.unicode.org/Public/$REV/ucd/Scripts.txt" \
    > ./Scripts.txt

type Range {
  let first: String
  let last: String
  let firstN: Int
  let lastN: Int
}

function try_merge(o : Range) : Range? for Range {
    if $this.lastN + 1 == $o.firstN {
        # merge range
        return new Range(
            $this.first,
            $o.last,
            $this.firstN,
            $o.lastN
        )
    }
    return $none
}

var sc_map : [String: [Range]]
var count =0 
for line in new FD('./Scripts.txt') {
  $count++
  ($line.empty() || $line.startsWith('#')) && continue
  var matched = $/^(?<first>[0-9A-F]+)(\.\.(?<last>[0-9A-F]+))?[ ]*;[ ]+(?<sc>([a-zA-Z][_a-zA-Z0-9]*)) +#.+$/.match($line) ?? {echo failed at $count: $line; exit 1;}
  var first = $matched.named('first')!
  var last =  $matched.named('last') ?? $first
  var firstN = $first.toInt(16)!
  var lastN = $last.toInt(16)!
  # var r = "{ 0x$first, 0x$last },"
  var sc = $matched.named('sc')!
  $sc_map.putIfAbsent($sc, new [Range]())
  $sc_map[$sc].add(new Range($first, $last, $firstN, $lastN))
}

# check cate
for _, values in $sc_map {
  assert ! $values.empty()
}

# merge
var scs : [String]
for sc, _ in $sc_map {
  $scs.add($sc)
}

for sc in $scs {
  var new_values: [Range]
  var old_values = $sc_map[$sc]
  for value in $old_values {
    if $new_values.empty() {
      $new_values.add($value)
      continue
    }
    if let v = $new_values.peek().try_merge($value) {
      $new_values.pop()
      $new_values.push($v)
      continue
    }
    $new_values.add($value)
  }
  $sc_map[$sc] = $new_values
}

# output
for sc, values in $sc_map {
  for v in $values {
    echo "{ 0x${v.first}, 0x${v.last} },"
  } with > $output_dir/sc_${$sc.lower()}.golden
  echo "sc:$sc" > $output_dir/sc_${$sc.lower()}.test
}