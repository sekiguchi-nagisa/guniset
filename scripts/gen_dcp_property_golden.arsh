#!/usr/bin/env arsh

var REV="16.0.0"

let output_dir = $1.realpath()

test -f ./DerivedCoreProperties.txt ||
  curl "https://www.unicode.org/Public/$REV/ucd/DerivedCoreProperties.txt" \
    > ./DerivedCoreProperties.txt

## extract property
var propertySet : [String:Bool]
var count =0 
for line in new FD('./DerivedCoreProperties.txt') {
  $count++
  ($line.empty() || $line.startsWith('#')) && continue
  var matched = $/^(?<first>[0-9A-F]+)(\.\.(?<last>[0-9A-F]+))?[ ]*;[ ]+(?<prop>([a-zA-Z][_a-zA-Z0-9]*))([ ]*;[ ]*(?<prop2>([a-zA-Z][_a-zA-Z0-9]*)))? +#.+$/.match($line) ?? {echo failed at $count: $line; exit 1;}
  var sc = $matched.named('prop')!
  if let r = $matched.named('prop2') {
    $sc += "_" + $r
  }
  $propertySet.putIfAbsent($sc, $true)
}
var properties : [String] 
for k, _ in $propertySet {
  $properties.add($k)
}
$properties.sort()

printf "%s\n" $properties > $output_dir/dcp.golden