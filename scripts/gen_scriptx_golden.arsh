#!/usr/bin/env arsh

source $SCRIPT_DIR/set_helper.arsh

# generate golden files for script (except for 'Unknown')

var REV="16.0.0"

let output_dir = $1.realpath()

test -f ScriptExtensions.txt ||
  curl "https://www.unicode.org/Public/$REV/ucd/ScriptExtensions.txt" \
    > ./ScriptExtensions.txt

var scx_map : [String: [Range]]
var count =0 
for line in new FD('./ScriptExtensions.txt') {
  $count++
  ($line.empty() || $line.startsWith('#')) && continue
  var matched = $/^(?<first>[0-9A-F]+)(\.\.(?<last>[0-9A-F]+))?[ ]*;(?<scx>( +[a-zA-Z][_a-zA-Z0-9]*)+) +#.+$/.match($line) ?? {echo failed at $count: $line; exit 1;}
  var first = $matched.named('first')!
  var last =  $matched.named('last') ?? $first
  var scxs = $matched.named('scx')!
  for scx in $scxs.split(' ') {
    $scx.empty() && continue
    $scx_map.putIfAbsent($scx, new [Range]())
    $scx_map[$scx].add(new Range($first, $last))
  }
}

# check cate
for _, values in $scx_map {
  assert ! $values.empty()
}

# minimize
$minimize($scx_map)

# output
for scx, values in $scx_map {
  for v in $values {
    echo "{ 0x${v.first}, 0x${v.last} },"
  } with > $output_dir/scx_${$scx.lower()}.golden
  echo "scx:$scx" > $output_dir/scx_${$scx.lower()}.test
}